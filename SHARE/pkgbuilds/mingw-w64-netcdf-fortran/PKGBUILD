
_realname=netcdf-fortran
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.5.2
pkgrel=1
pkgdesc="NetCDF fortran bindings (mingw-w64)"
arch=('any')
options=('staticlibs' '!buildflags' '!strip')
#options=('!buildflags' '!strip')
license=('custom')
url="http://www.unidata.ucar.edu/downloads/netcdf/netcdf-fortran/index.jsp"
depends=("${MINGW_PACKAGE_PREFIX}-netcdf")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/Unidata/netcdf-fortran/archive/v${pkgver}.tar.gz")

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  sed -i "s|libnetcdff_la_LDFLAGS = -version-info|libnetcdff_la_LDFLAGS = -no-undefined -version-info|g" fortran/Makefile.am
  autoreconf -vfi
}

build() {
  [[ -d ${srcdir}/build-static-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-static-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-static-${MINGW_CHOST} && cd ${srcdir}/build-static-${MINGW_CHOST}

  #MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  #${MINGW_PREFIX}/bin/cmake \
  #  -G "MSYS Makefiles" \
  #  -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
  #  -DCMAKE_BUILD_TYPE=Release \
  #  -DBUILD_SHARED_LIBS=OFF \
  #  "${srcdir}/netcdf-fortran-${pkgver}"
  "${srcdir}/${_realname}-${pkgver}"/configure \
    --prefix=${MINGW_PREFIX} \
    --build="$CHOST" \
    --disable-shared
  make

  [[ -d ${srcdir}/build-shared-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-shared-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-shared-${MINGW_CHOST} && cd ${srcdir}/build-shared-${MINGW_CHOST}

  "${srcdir}/${_realname}-${pkgver}"/configure \
    --prefix=${MINGW_PREFIX} \
    --build="$CHOST" \
    --enable-shared \
    --enable-dll
  make
}

package() {
  cd ${srcdir}/build-static-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  cd ${srcdir}/build-shared-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  ${MINGW_PREFIX}/bin/strip --strip-unneeded ${pkgdir}${MINGW_PREFIX}/bin/*.dll
  ${MINGW_PREFIX}/bin/strip -g ${pkgdir}${MINGW_PREFIX}/lib/*.a

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYRIGHT \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYRIGHT

  local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})
  find ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig -name *.pc -exec sed -i -e"s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" {} \;
}
